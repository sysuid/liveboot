apply plugin: 'com.android.application'

android {
    compileSdk 34
    buildToolsVersion '34.0.0'
    defaultConfig {
        applicationId "eu.chainfire.liveboot"
        minSdkVersion 21
        targetSdk 34
        versionCode 186
        versionName '1.86'
    }
    signingConfigs {
        debug {
//            storeFile file(System.properties['keystoreDebug'])
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
        }
        debug {
            signingConfig signingConfigs.debug
        }
    }
    namespace 'eu.chainfire.liveboot'
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            if (variant.buildType.name == 'release') {
                outputFileName = "LiveBoot" + "-v" + android.defaultConfig.versionName + "-" + (new Date()).format('yyyyMMddHHmmss') + ".apk"
                variant.getAssembleProvider().configure() {
                    it.doLast {
                        copy {
                            from 'build/outputs/mapping/release'
                            into 'proguard'
                            include '**/mapping.txt'
                        }
                    }
                }
            }
        }
    }
}

dependencies {
    implementation 'androidx.legacy:legacy-support-core-utils:1.0.0'
    implementation 'androidx.annotation:annotation:1.6.0'
    implementation 'eu.chainfire:libsuperuser:1.1.1'
    implementation 'eu.chainfire:librootjava:1.3.3'
    implementation 'eu.chainfire.librootjava:librootjavadaemon:1.3.3'
    implementation 'eu.chainfire:libcfsurface:1.1.0'
    implementation 'com.android.billingclient:billing:6.0.0'
}
